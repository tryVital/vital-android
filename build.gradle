import groovy.json.JsonOutput

buildscript {
    ext {
        core = '1.9.0'
        compose = '1.4.1'

        // Linked with Kotlin compiler version
        compose_compiler = '1.4.8'

        nav = '2.5.3'
        material3 = '1.0.1'
        activity_compose = '1.6.1'
        lifecycle_runtime = '2.5.1'
        lifecycle_viewmodel_compose = '2.5.1'
        lifecycle_livedata_ktx = '2.6.1'
        lifecycle_process = '2.7.0'
        browser = '1.4.0'
        retrofit = '2.9.0'
        retrofit2_kotlin_coroutines_adapter = '0.9.2'
        moshi = '1.14.0'
        okhttp = '4.10.0'
        junit = '4.13.2'
        test_ext = '1.1.4'
        test_espresso = '3.5.1'
        mockito = "4.8.0"
        mockito_kotlin = "4.0.0"
        mockito_inline = "4.6.1"
        mockwebserver = "4.10.0"
        kotlinx_coroutines_test = "1.6.4"
        health_connect = "1.1.0-alpha06"
        coil_compose = "2.0.0"
        ble = "2.5.1"
        annotation = "1.5.0"
        startup_runtime = "1.1.1"
        security_crypto = "1.0.0"
        kotlin = '1.8.22'
        ksp = '1.8.22-1.0.11'
        kotlinx_coroutines = "1.6.4"
        work_runtime = "2.7.1"
    }
}

plugins {
    // Android Gradle Plugin 7.3.0 (not recommended)
    // id 'com.android.application' version '7.3.0' apply false
    // id 'com.android.library' version '7.3.0' apply false

    // Android Gradle Plugin 8.2.0
    id 'com.android.application' version '8.2.0' apply false
    id 'com.android.library' version '8.2.0' apply false

    id 'org.jetbrains.kotlin.android' version '1.8.22' apply false

    id 'org.jreleaser' version '1.18.0'
}

def localProperties = new Properties()
def localPropertiesFile = project.rootProject.file("local.properties")
if (localPropertiesFile.exists()) {
    localProperties.load(localPropertiesFile.newDataInputStream())
}

subprojects {
    apply plugin: 'maven-publish'
    apply plugin: 'signing'

    publishing {
        repositories {
            maven {
                url = rootProject.layout.buildDirectory.dir('staging-deploy')
            }
        }
    }

    jreleaser {
        signing {
            active = 'ALWAYS'
            armored = true
        }
        deploy {
            maven {
                mavenCentral {
                    sonatype {
                        active = 'ALWAYS'
                        url = 'https://central.sonatype.com/api/v1/publisher'
                        stagingRepository('build/staging-deploy')
                        verifyPom = false
                    }
                }
            }
        }
    }

    afterEvaluate {
        signing {
            useInMemoryPgpKeys(
                (localProperties.getProperty("signing.secretKey") ?: System.getenv("JRELEASER_GPG_SECRET_KEY"))?.replace("\\n", "\n"),
                localProperties.getProperty("signing.password") ?: System.getenv("JRELEASER_GPG_PASSPHRASE")
            )
            sign publishing.publications
        }
    }
}

task clean(type: Delete) {
    delete rootProject.buildDir
}
